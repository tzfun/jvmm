ext.projectName = 'jvmm-server'
archivesBaseName = "jvmm-server"

group = rootProject.group
version = rootProject.version

dependencies {
    compile(project(':core'))
    compile(project(':convey'))
    compile "org.yaml:snakeyaml:${snakeyamlVersion}"
}

tasks.compileJava.configure {
    dependsOn project(':logger').tasks.jar

    doLast {
        println("copy logger jar file to resources")
        copy {
            from '../logger/build/libs/jvmm-logger.jar'
            into 'src/main/resources'
        }
    }
}

jar {
    manifestContentCharset 'utf-8'
    metadataCharset 'utf-8'

    archiveName("jvmm-server.jar")

    manifest {
        attributes 'Main-Class': 'org.beifengtz.jvmm.server.ServerApplication'
    }

    //  发布时去掉
//    from {
//        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
//    }

    doLast {
        def targetDir = "${rootProject.rootDir}/bin/jvmm-${rootProject.version}"

        copy {
            println("copy config.yml to ${targetDir}")

            from "${project.buildDir}/resources/main/config.yml"
            into targetDir
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    getArchiveClassifier().set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    getArchiveClassifier().set('javadoc')
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name projectName
                packaging 'jar'
                description 'Provides access to operating system, process, thread and other information during Java runtime.'
                url 'https://github.com/tzfun/jvmm'

                scm {
                    url = 'https://github.com/tzfun/jvmm'
                    connection = 'scm:git:git://github.com/tzfun/jvmm.git'
                    developerConnection = 'scm:git:ssh://git@github.com/tzfun/jvmm.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name 'beifengtz'
                        email 'beifengtz@163.com'
                    }
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}

javadoc {
    options.addStringOption("charset", "UTF-8")
}
