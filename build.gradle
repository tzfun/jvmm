//  新版必须和namespace相同
group = 'io.github.tzfun.jvmm'
version = '2.5.0'

ext {
    nettyVersion = "4.1.92.Final"
    nettyNativeSslVersion = "2.0.41.Final"
    junitJupiterVersion = "5.7.0"
    gsonVersoion = "2.10.1"
    cfrVersion = "0.152"
    oshiVersion = "6.4.5"
    snakeyamlVersion = "1.33"
    asmVersion = "9.5"
    protobufVersion = "3.25.2"
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'application'

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    compileJava {
        options.compilerArgs << "-parameters" << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://maven.aliyun.com/repository/public/' }
        mavenCentral()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    }

    test {
        useJUnitPlatform()
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

def libProjects() {
    return subprojects.findAll { !it.name.matches("demo|client|agent") }
}


import java.nio.file.Files
import java.nio.file.StandardCopyOption

configure(libProjects()) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    def moduleName = it.name
    def projectName = "jvmm-${it.name}"

    if (env == 'publish') {

//        java {
//            withSourcesJar()
//            withJavadocJar()
//        }

        task sourcesJar(type: Jar) {
            from sourceSets.main.allSource
            archiveClassifier = 'sources'
            dependsOn classes
        }

        task javadocJar(type: Jar) {
            archiveClassifier = 'javadoc'
            from javadoc.destinationDir
            dependsOn javadoc
        }

        tasks.jar.configure {
            dependsOn sourcesJar
        }

        tasks.withType(Javadoc).configureEach {
            options.version = true
            options.author = true
            options.encoding = "UTF-8"
            options.charSet = "UTF-8"
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java

                    artifact sourcesJar
                    artifact javadocJar

                    artifactId = projectName

                    pom {
                        name = projectName
                        packaging = 'jar'
                        description = 'Provides access to operating system, process, thread and other information during Java runtime.'
                        url = 'https://github.com/tzfun/jvmm'

                        scm {
                            url = 'https://github.com/tzfun/jvmm'
                            connection = 'scm:git:git://github.com/tzfun/jvmm.git'
                            developerConnection = 'scm:git:ssh://git@github.com/tzfun/jvmm.git'
                        }

                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }

                        developers {
                            developer {
                                name = 'beifengtz'
                                email = 'beifengtz@163.com'
                            }
                        }
                    }
                }
            }

            repositories {
                //  API调试： https://ossrh-staging-api.central.sonatype.com/swagger-ui/
                maven {
                    name = "ossrh-staging-api"
//                    name = "sonatypeCentralStagingApi"
                    url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"

                    credentials {
                        username = mavenCentralUsername
                        password = mavenCentralPassword
                    }
                }
                //  发布到本地
//                maven {
//                    name = "localBundle"
//                    url = uri("${buildDir}/central-repo")
//                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }

        tasks.register('generateCentralBundle') {
            group = "publishing"
            description = "Generate Central Portal bundle.zip"

            doLast {
                def repoDir = file("${buildDir}/central-repo")
                def bundleDir = file("${buildDir}/central-bundle")

                bundleDir.deleteDir()
                bundleDir.mkdirs()

                copy {
                    from repoDir
                    into bundleDir
                    exclude "**/maven-metadata.xml*"
                    exclude "**/*.sha256"
                    exclude "**/*.sha512"
                }

                ant.zip(destfile: "${buildDir}/central-bundle.zip") {
                    fileset(dir: bundleDir)
                }

                println "✔ Central bundle.zip ready"
            }
        }

        tasks.register('publishCentralBundle') {
            group = "publishing"
            description = "Upload bundle.zip to Central Portal and auto publish"

            dependsOn 'generateCentralBundle'

            doLast {
                def auth = "${mavenCentralUsername}:${mavenCentralPassword}".bytes.encodeBase64().toString()
                def namespace = "io.github.tzfun"
                def url = "https://ossrh-staging-api.central.sonatype.com/manual/upload/defaultRepository/${namespace}?publishing_type=automatic"
                println("request to ${url}")
                def cmd = [
                        "curl",
                        "-sS",
                        "--http1.1",
                        "-X", "POST",
                        "-H", "Authorization: Bearer ${auth}",
                        url
                ]

                println "▶ Uploading bundle to Central Portal..."
                def process = cmd.execute()
                process.in.eachLine { println it }
                process.err.eachLine { System.err.println(it) }

                if (process.waitFor() != 0) {
                    throw new GradleException("Central Portal upload failed")
                }

                println "✔ Central Portal upload finished"
            }
        }

        tasks.register('releaseToMavenCentral') {
            group = "publishing"
            description = "Build, publish and auto release to Maven Central"

            dependsOn(
                    ":${moduleName}:publish",
                    "publishCentralBundle"
            )
        }
    }
}

